apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'walle'

def config = rootProject.ext
android {
    compileSdkVersion config.android.compileSdkVersion
    buildToolsVersion config.android.buildToolsVersion

    defaultConfig {
        applicationId config.android.applicationId
        minSdkVersion config.android.minSdkVersion
        targetSdkVersion config.android.targetSdkVersion
        versionCode config.android.versionCode
        versionName config.android.versionName
    }

    signingConfigs {
        myConfig {
            keyAlias 'coolxtc'
            keyPassword 'coolxtc123'
            storeFile file("../key.jks")
            storePassword 'coolxtc123'
            //现阶段渠道包需要关闭v2验证
//            v2SigningEnabled false
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "BaseApp-default-${variant.buildType.name}-v${variant.versionName}.apk".replace("_", "")
        }
    }

    buildTypes {
        //调试版本
        debug {
            debuggable true
            signingConfig signingConfigs.myConfig
            minifyEnabled false
            shrinkResources false
        }
        //内部测试版本
        alpha {
            debuggable true
            signingConfig signingConfigs.myConfig
            minifyEnabled false
            shrinkResources false
        }
//        //发布版本
        release {
            debuggable false
            signingConfig signingConfigs.myConfig
            minifyEnabled true
            shrinkResources true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
    mavenCentral()
}

//ARouter路由配置
kapt {
    arguments {
        arg("AROUTER_MODULE_NAME", project.getName())
    }
}

walle {
    // 指定渠道包的输出路径
    apkOutputFolder = new File("${project.buildDir}/outputs/channels");
    // 定制渠道包的APK的文件名称
    apkFileNameFormat = '${appName}-${packageName}-${channel}-${buildType}-v${versionName}-${versionCode}-${buildTime}.apk';
    // 渠道配置文件
    channelFile = new File("${project.getProjectDir()}/channel")
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    // walle 渠道包生成插件
    implementation 'com.meituan.android.walle:library:1.1.7'
    //模块化 start
    implementation project(':base:common')

    if (!isModule.toBoolean()) {
        implementation project(':business:module-answer')
        implementation project(':business:module-dev')
        implementation project(':business:module-index')
    }
    //模块化 end
    //ARouter
    kapt "com.alibaba:arouter-compiler:${config.dependVersion.arouterCompilerVersion}"
}
